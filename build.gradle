apply plugin: 'java'
apply plugin: 'signing'
apply plugin: 'maven'
apply plugin: 'application'

sourceCompatibility = '1.8'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

//apply from: 'http://gradle-plugins.mihosoft.eu/latest/vlicenseheader.gradle'
//repairHeaders.licenseHeaderText = new File(projectDir,'./license-template.txt')

Properties props = new Properties()
props.load(new FileInputStream(buildDir.getAbsolutePath()+"/../src/main/resources/com/neuronrobotics/javacad/build.properties"))

task wrapper(type: Wrapper, description: 'Creates and deploys the Gradle wrapper to the current directory.') {
    gradleVersion = '2.1'
}

if (!hasProperty('mainClass')) {
    ext.mainClass = 'eu.mihosoft.vrl.v3d.Main'
}

mainClassName = mainClass

repositories {
    mavenCentral()
}


dependencies {
    
    testCompile group: 'junit', name: 'junit', version: '4.0'

    compile group: 'java3d', name: 'vecmath', version: '1.3.1'
    compile 'org.slf4j:slf4j-simple:1.6.1'
    //compile group: 'java3d', name: 'vecmath', version: '1.3.1' , classifier: "javadoc"
}

// create a fat-jar (class files plus dependencies
// excludes VRL.jar (plugin jar files must not start with 'vrl-\\d+')
jar {

	doFirst { 
	    // dependencies except VRL
	    from (configurations.runtime.asFileTree.
	        filter({file->return !file.name.startsWith("vrl-0")}).
	        files.collect { zipTree(it) } ){
	    	exclude 'META-INF/MANIFEST.MF'
			exclude 'META-INF/*.SF'
			exclude 'META-INF/*.DSA'
			exclude 'META-INF/*.RSA'
	    }
	}

    // project class files compiled from source
    from files(sourceSets.main.output.classesDir)

}

group = "com.neuronrobotics"
archivesBaseName = props."app.name"
version = props."app.version"

task javadocJar(type: Jar) {
	classifier = 'javadoc'
	from javadoc
}

task sourcesJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

artifacts {
	archives javadocJar, sourcesJar, jar
}

signing {
	sign configurations.archives
}
uploadArchives {
	repositories {
	  mavenDeployer {
		beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
  
		repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
		  authentication(userName: ossrhUsername, password: ossrhPassword)
		}
  
		snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
		  authentication(userName: ossrhUsername, password: ossrhPassword)
		}
  
		pom.project {
		  name 'JCSG JavaCad'
		  packaging 'jar'
		  // optionally artifactId can be defined here
		  description 'A Java based CSG Cad library'
		  url 'http://neuronrobotics.com'
  
		  scm {
			connection 			'scm:git:https://github.com/ThreeCSG/JCSG'
			developerConnection 'scm:git:git@github.com:ThreeCSG/JCSG.git'
			url 'https://github.com/ThreeCSG/JCSG'
		  }
  
		  licenses {
			license {
			  name 'The Apache License, Version 2.0'
			  url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
			}
		  }
  
		  developers {
			developer {
			  id 'madhephaestus'
			  name 'Kevin Harrington'
			  email 'kharrington@neuronrobotics.com'
			}
		  }
		}
	  }
	}
  }
